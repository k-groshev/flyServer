import java.text.SimpleDateFormat

buildscript {
    repositories {
        mavenLocal()
        jcenter()
        mavenCentral()
        maven { url "https://plugins.gradle.org/m2/" }
        maven { url 'http://repo.spring.io/milestone' }
    }
    dependencies {
        classpath 'com.ofg:uptodate-gradle-plugin:+'
        classpath 'org.ajoberstar:gradle-git:1.4.2'

//    classpath 'org.kt3k.gradle.plugin:coveralls-gradle-plugin:1.0.2'
//    classpath "org.sonarqube.gradle:gradle-sonarqube-plugin:1.0"
    }
}

apply plugin: 'java'
apply plugin: 'idea'
apply plugin: 'jacoco'

sourceCompatibility = 1.8
targetCompatibility = 1.8

compileJava.options.encoding = 'UTF-8'
configurations { providedCompile }


dependencies {
    compile(
            project(':common'),
            "commons-fileupload:commons-fileupload:$commonsFileUploadVersiion",
            "org.glassfish.jersey.core:jersey-client:$jerseyVersion",
            "org.glassfish.jersey.media:jersey-media-json-jackson:$jerseyVersion",
            "commons-codec:commons-codec:$commonsCodecVersion",
            "log4j:log4j:$log4jVersion",

            //ehcache
            "net.sf.ehcache:ehcache:$ehcacheVersion",

            // Hibernate
            "org.hibernate:hibernate-core:$hibernateVersion",
            "org.hibernate:hibernate-entitymanager:$hibernateVersion",
            "org.hibernate:hibernate-ehcache:$hibernateVersion",
            "org.hibernate:hibernate-java8:$hibernateVersion",

            // Spring
            "org.springframework:spring-webmvc:$springVersion",
            "org.springframework:spring-beans:$springVersion",
            "org.springframework:spring-core:$springVersion",
            "org.springframework:spring-web:$springVersion",
            "org.springframework:spring-context-support:$springVersion",
            "org.springframework:spring-orm:$springVersion",
            "org.springframework:spring-oxm:$springVersion",
            "org.springframework:spring-expression:$springVersion",
            "org.springframework:spring-aop:$springVersion",
            "org.springframework:spring-aspects:$springVersion",

            //Spring AOP dependency
            "cglib:cglib:$cglibVersion",
            "org.aspectj:aspectjweaver:$aspectjVersion",

            //spring related
            "javax.annotation:jsr250-api:$jsr250Version",

            //Spring Security
            "org.springframework.security:spring-security-core:$springSecurityVersion",
            "org.springframework.security:spring-security-web:$springSecurityVersion",
            "org.springframework.security:spring-security-config:$springSecurityVersion",

            // database
            "org.liquibase:liquibase-core:$liquibaseVersion",
            "org.springframework.data:spring-data-jpa:$springDataVersion",
            "org.postgresql:postgresql:$postgresVersion",

            // for mail
            "com.typesafe:config:$typesafeVersion",
            "org.freemarker:freemarker:$freemarkerVersion",
            "javax.mail:mail:$javaxMailVersion",

            // rest-api documentation
            "io.springfox:springfox-swagger2:$swaggerVersion",
            "io.springfox:springfox-spring-web:$swaggerVersion",

            //validation
            "javax.validation:validation-api:$javaxValidationVersion",
            "org.hibernate:hibernate-validator:$hibernateValidatorVersion",

            //jsoup HTML parser library @ http://jsoup.org/
            "org.jsoup:jsoup:$jsoupVersion",

            //Core JAR is absolutely necessary to use Simons (Stopwatch, etc.)
            "org.javasimon:javasimon-core:$javasimonVersion",
            // This one allows monitoring JDBC calls (proxy driver)
            "org.javasimon:javasimon-jdbc41:$javasimonVersion",
            //JavaEE support, servlet filter, EJB/CDI interceptor
            "org.javasimon:javasimon-javaee:$javasimonVersion",
            //Spring support, AOP interceptor, MVC handler interceptor
            "org.javasimon:javasimon-spring:$javasimonVersion",
            //Embedded Java Simon web console
            "org.javasimon:javasimon-console-embed:$javasimonVersion",

/*
    //for rest doc controller
    "com.google.guava:guava:",
    "org.apache.commons:commons-lang3:$commonsLangVersion",
    "org.apache.commons:commons-io:",
    "com.github.markusbernhardt:xml-doclet:",
    "org.ocpsoft.prettytime:prettytime:$prettyTimeVersion"
    "org.fusesource.jansi:jansi:"
 */
            "com.github.stephenc.high-scale-lib:high-scale-lib:1.1.4",

    )

    runtime(
            "log4j:log4j:$log4jVersion",
            "org.slf4j:slf4j-api:$slf4jVersion",
            "org.slf4j:jcl-over-slf4j:$slf4jVersion",
            "org.slf4j:log4j-over-slf4j:$slf4jVersion",
            "com.mattbertolini:liquibase-slf4j:$slf4jLiquibaseVersion",
            "ch.qos.logback:logback-classic:$logbackVersion",
            "ch.qos.logback:logback-core:$logbackVersion"
    )

    testCompile(
            "junit:junit:$junitVersion",
            "org.springframework:spring-test:$springVersion",
            "org.mockito:mockito-all:$mokitoVersion",
            "io.springfox:springfox-staticdocs:$swaggerVersion",
            "javax.transaction:javax.transaction-api:$javaxTransactionVersion",
            "javax.el:javax.el-api:$javaxElVersion",
            //"com.fasterxml:classmate:$fasterxmlClassmateVersion",
            "org.codehaus.groovy:groovy-all:$groovyVersion"
    )

    providedCompile(
            // servlet api
            "javax.servlet:javax.servlet-api:$servlepApiVersion"
    )
}


task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives sourcesJar
//  archives javadocJar
}

task addHashFile << {
    ext.repo = org.ajoberstar.grgit.Grgit.open("${project.rootDir}/..")
    def headCommit = repo.head()
    def status = repo.status()
    def tags = repo.tag.list()
    def remotes = repo.remote.list()
    ext.gitBranch = repo.branch.current.name
    ext.gitCommitId = headCommit.id
    ext.gitCommitIdAbbrev = headCommit.getAbbreviatedId(7)
    if (!status.isClean()) {
        ext.gitCommitIdDesc = headCommit.abbreviatedId + '-dirty'
        ext.gitCommitIdDescShort = headCommit.abbreviatedId + '-dirty'
    } else {
        ext.gitCommitIdDesc = headCommit.abbreviatedId
        ext.gitCommitIdDescShort = headCommit.abbreviatedId
    }
    ext.gitCommitMessageFull = headCommit.fullMessage.replaceAll("\\r\\n|\\r|\\n", "\\\\n")
    ext.gitCommitMessageShort = headCommit.shortMessage
    ext.gitCommitTime = new Date(headCommit.time * 1000L).format("dd.MM.yyyy @ HH\\:mm\\:ss z")
    SimpleDateFormat dateFormat = new SimpleDateFormat("EEE MMM dd HH:mm:ss z yyyy", Locale.ENGLISH);
    ext.gitNowTime = dateFormat.format(new Date())
    ext.gitCommitUserEmail = headCommit.author.email
    ext.gitCommitUserName = headCommit.author.name
    ext.gitBuildTime = new Date().format("dd.MM.yyyy @ HH\\:mm\\:ss z")
    ext.gitDirty = !status.isClean()
    ext.gitBuildUserName = repo.repository.jgit.repo.config.getString('user', null, 'name')
    ext.gitBuildUserEmail = repo.repository.jgit.repo.config.getString('user', null, 'email')
    ext.gitTags = ''
    tags.each { t ->
        ext.gitTags = ext.gitTags + "$t.fullName" + ',';
    }
    ext.gitROURL = ''
    repo.remote.list().each { w ->
        if ("origin" == "$w.name") {
            if (ext.gitROURL == '') {
                ext.gitROURL = ext.gitROURL + "$w.url".replace(":", "\\:")
            } else {
                ext.gitROURL = ext.gitROURL + "$w.url".replace(":", "\\:") + ','
            }
        }
    }

    File resourcesMainDir = new File(project.buildDir, 'resources/main')
    if (!resourcesMainDir.exists()) {
        resourcesMainDir.mkdirs()
    }
    File gitPropertiesFile = new File(resourcesMainDir, 'git.properties')
    if (!gitPropertiesFile.exists()) {
        gitPropertiesFile.createNewFile()
    }

    def keys = ["git.tags"
                , "git.commit.id"
                , "git.commit.id.abbrev"
                , "git.commit.id.describe"
                , "git.commit.id.describe-short"
                , "git.commit.message.full"
                , "git.commit.message.short"
                , "git.commit.user.name"
                , "git.commit.user.email"
                , "git.commit.time"
                , "git.build.user.name"
                , "git.build.user.email"
                , "git.build.time"
                , "git.branch"
                , "git.dirty"
                , "git.remote.origin.url"]


    def map = ["git.tags"                      : "${gitTags}"
               , "git.commit.id"               : "${gitCommitId}"
               , "git.commit.id.abbrev"        : "${gitCommitIdAbbrev}"
               , "git.commit.id.describe"      : "${gitCommitIdDesc}"
               , "git.commit.id.describe-short": "${gitCommitIdDescShort}"
               , "git.commit.message.full"     : "${gitCommitMessageFull}"
               , "git.commit.message.short"    : "${gitCommitMessageShort}"
               , "git.commit.user.name"        : "${gitCommitUserName}"
               , "git.commit.user.email"       : "${gitCommitUserEmail}"
               , "git.commit.time"             : "${gitCommitTime}"
               , "git.build.user.name"         : "${gitBuildUserName}"
               , "git.build.user.email"        : "${gitBuildUserEmail}"
               , "git.build.time"              : "${gitBuildTime}"
               , "git.branch"                  : "${gitBranch}"
               , "git.dirty"                   : "${gitDirty}"
               , "git.remote.origin.url"       : "${gitROURL}"]

    gitPropertiesFile.withWriter('UTF-8') { w ->
        w.writeLine "#Generated by gradle with love"
        w.writeLine "#${gitNowTime}"
        map.subMap(keys).each { key, value ->
            w.writeLine "$key=$value"
        }
    }
}


jar {
//    dependsOn 'addHashFile'
    baseName = "service"
    version = "1.0-SNAPSHOT"
}

sourceSets.main.compileClasspath += configurations.providedCompile
sourceSets.test.compileClasspath += configurations.providedCompile
sourceSets.test.runtimeClasspath += configurations.providedCompile

jacoco {
    toolVersion = "0.7.5.201505241946"
    reportsDir = file("$buildDir/reports/jacoco")
}


test{
    jacoco {
        append = false
        destinationFile = file("$buildDir/jacoco/jacocoTest.exec")
        classDumpFile = file("$buildDir/jacoco/classpathdumps")
    }
}

jacocoTestReport {
    reports {
        xml.enabled true
        csv.enabled false
        html.enabled true
        html.destination "${buildDir}/reports/jacocoHtml"
    }
}

/*
task prod << {
    bootRun.systemProperty 'spring.profiles.active', 'prod'
}

task mac << {
    bootRun.systemProperty 'spring.profiles.active', 'mac'
}

task dev << {
    bootRun.systemProperty 'spring.profiles.active', 'dev'
}*/
